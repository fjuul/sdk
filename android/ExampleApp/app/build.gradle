plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    namespace 'com.fjuul.sdk.android.exampleapp'
    compileSdkVersion 36

    defaultConfig {
        applicationId "com.fjuul.sdk.android.exampleapp"
        minSdk 26
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            if (project.hasProperty('DEBUG_STORE_FILE')) {
                storeFile file(DEBUG_STORE_FILE)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEY_ALIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
}

tasks.register('publishFjuulSDKToMavenLocal', Exec) {
    workingDir "$rootDir/../"
    def publishingCommand = './gradlew publishToMavenLocal -PVERSION_NAME=105.1.1'
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', publishingCommand
    } else {
        commandLine 'sh', '-c', publishingCommand
    }
}
preBuild.dependsOn('publishFjuulSDKToMavenLocal')

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.2.20"
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.fragment:fragment-ktx:1.8.9"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.3'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.3'
    implementation "androidx.fragment:fragment-ktx:1.8.9"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    //noinspection GradleDynamicVersion
    implementation 'com.fjuul.sdk:analytics:+'
    //noinspection GradleDynamicVersion
    implementation 'com.fjuul.sdk:user:+'
    //noinspection GradleDynamicVersion
    implementation 'com.fjuul.sdk:activitysources:+'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    // this dependency is required to support date picker in lower version
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.5")
}
