buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.4.0"
}

ext {
    compileSdkVersion = 29
    buildToolsVersion = "30.0.2"
    minSdkVersion = 21
    targetSdkVersion = 29

    okHttpVersion = '4.9.1'
    retrofitVersion = '2.9.0'
    androidXAnnotationVersion = '1.1.0'
    androidXCoreVersion = '1.3.2'
    robolectricVersion = '4.5'
    androidXTestCoreVersion = '1.3.0'
    junitVersion = '4.13'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '*/build/generated/**/*.*'
        }
        // licenseHeaderFile project.rootProject.file('spotless/copyright.java')

        // optional: you can specify a specific version and/or config file
        eclipse('4.13.0').configFile('eclipse-java-google-style.xml')
        removeUnusedImports()
        trimTrailingWhitespace()
        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', ''	// A sequence of package names
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
        }
        greclipse()
        indentWithSpaces(4)
    }

    // only for the example application
    kotlin {
        target fileTree('./ExampleApp') {
            include '**/*.kt'
        }
        // version and userData are both optional
        // NOTE: keep synced with editorconfig!
        def styles = [
            indent_size: '4',
            indent_style: 'space',
            'continuation_indent_size': '2',
            'insert_final_newline': 'true',
            trim_trailing_whitespace: 'true',
            end_of_line: 'lf'
        ]
        ktlint('0.37.2').userData(styles)
    }
}

configure(subprojects) {
    apply plugin: 'com.android.library'
    android {
        lintOptions {
            enable 'Interoperability'
            // Interoperability issues
            error 'UnknownNullness', 'NoHardKeywords', 'LambdaLast', 'KotlinPropertyAccess'
            explainIssues true
            textReport true
            xmlReport System.getenv("CI") != "true"
            htmlReport System.getenv("CI") != "true"
        }
    }
}



allprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = [
                "passed",
                "skipped",
                "failed",
                "standardOut"
            ]
        }
    }
}

repositories {
    google()
}
