plugins {
    id 'com.android.library'
}
apply from: "${rootDir}/gradle/publish.gradle"

android {
    namespace 'com.fjuul.sdk.core'
    compileSdkVersion rootProject.ext.compileSdk

    defaultConfig {
        minSdk rootProject.ext.minSdk
        compileSdkVersion rootProject.ext.compileSdk
        targetSdk rootProject.ext.targetSdk

        versionCode 1
        versionName project.findProperty("VERSION_NAME") ?: "dev build"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
        }
        release {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        // Android Gradle plugin
        singleVariant("release")
    }
}

dependencies {
    def okHttpVersion = rootProject.ext.okHttpVersion
    def retrofitVersion = rootProject.ext.retrofitVersion
    api('com.squareup.okhttp3:okhttp') {
        version {
            // NOTE: since Android 5 support, https://square.github.io/okhttp/changelog_3x/#version-3130
            strictly '[3.13.0, 5)'
            prefer okHttpVersion
        }
    }
    // used for logs from okhttp
    api "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    api "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    api 'com.squareup.moshi:moshi:1.15.2'
    api 'com.squareup.moshi:moshi-kotlin:1.15.2'
    api 'com.squareup.moshi:moshi-adapters:1.15.2'
    api 'com.jakewharton.timber:timber:5.0.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "androidx.annotation:annotation:$rootProject.ext.androidXAnnotationVersion"

    // Use JUnit test framework
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation project(':test')
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "androidx.test:core:$rootProject.ext.androidXTestCoreVersion"
}
