buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.4.0"
}

ext {
    okHttpVersion = '3.12.12'
    compileSdkVersion = 29
    buildToolsVersion = "29.0.3"
    minSdkVersion = 19
    targetSdkVersion = 29
    androidXAnnotationVersion = '1.1.0'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
        }
        // licenseHeaderFile project.rootProject.file('spotless/copyright.java')

        // optional: you can specify a specific version and/or config file
        eclipse('4.13.0').configFile('eclipse-java-google-style.xml')
        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', ''	// A sequence of package names
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
        }
        greclipse()
        indentWithSpaces(4)
    }
}

configure(subprojects) {
    apply plugin: 'com.android.library'
    android {
        lintOptions {
            enable 'Interoperability'
            // Interoperability issues
            error 'UnknownNullness', 'NoHardKeywords', 'LambdaLast', 'KotlinPropertyAccess'
            explainIssues true
            textReport true
        }
    }
}



allprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = [
                "passed",
                "skipped",
                "failed",
                "standardOut"
            ]
        }
    }
}

repositories {
    google()
}
